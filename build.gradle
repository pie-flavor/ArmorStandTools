buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'flavor.pie.promptsign' version '1.1.0'
}

group 'flavor.pie'
version '1.1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
    maven {
        url 'https://jitpack.io/'
    }
    maven {
        url 'https://repo.codemc.org/repository/maven-public'
    }
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    kapt 'org.spongepowered:spongeapi:7.1.0'
    shadow 'com.github.pie-flavor:kludge:1918cb5'
    shadow 'org.bstats:bstats-sponge-lite:1.4'
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar.enabled = false
build.dependsOn shadowJar
signArchives.dependsOn shadowJar

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
    relocate 'flavor.pie.kludge', 'flavor.pie.armorstandtools.util.kludge'
    relocate 'kotlin', 'flavor.pie.armorstandtools.runtime.kotlin'
    relocate 'org.bstats.sponge', 'flavor.pie.armorstandtools.metrics'
}
